# Snakemake file for running mothur pipeline in Close_AbxVirome_XXXX_2019

# NOTE: Change these settings before running workflow
mothurMock = ['Mock1-Mock2-Mock3-Mock4']
mothurControl = ['NA-Water-Water2-Water3-Water4']
mothurAlpha = ['nseqs','coverage','invsimpson','shannon','sobs']
mothurBeta = ['sharedsobs','thetayc','braycurtis']

# Leave these settings as is
mothurSamples = list(set(glob_wildcards(os.path.join('data/raw/', '{sample}_{readNum, R[12]}_001.fastq.gz')).sample))
readNum = ['R1', 'R2']
mothurGroups = ['sample','mock','control']



rule all:
	input:
		expand("data/process/{group}.final.count.summary",
			group = mothurGroups),
		expand("data/process/{group}.final.0.03.subsample.shared",
			group = ['sample','mock']),
		"data/process/sample.final.groups.rarefaction",
		"data/process/sample.final.groups.ave-std.summary",
		expand("data/process/sample.final.{beta}.0.03.lt.ave.dist",
			beta = mothurBeta),
		expand("data/process/sample.final.{beta}.0.03.lt.ave.nmds.axes",
			beta = mothurBeta),
		expand("data/process/sample.final.{beta}.0.03.lt.ave.pcoa.axes",
			beta = mothurBeta),
		"data/process/error_analysis/errorinput.pick.error.summary"
	output:
		timestamp=".subworkflow_timestamp"
	shell:
		"""
		mkdir -p logs/mothur/
		mv mothur*logfile logs/mothur/
		touch {output.timestamp}
		"""





##################################################################
#
# Part 1: Generate Reference and Mock Control Files
#
##################################################################

# Add log file
# ~15 min with 8GB
rule get16SReferences:
	input:
		script="code/bash/mothurReferences.sh"
	output:
		silvaV4="data/references/silva.v4.align",
		rdpFasta="data/references/trainset16_022016.pds.fasta",
		rdpTax="data/references/trainset16_022016.pds.tax"
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script}"


# Add log file
# ~15 min with 8GB
rule get16SMock:
	input:
		script="code/bash/mothurMock.sh",
		silvaV4=rules.get16SReferences.output.silvaV4
	output:
		mockV4="data/references/zymo.mock.16S.v4.fasta"
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script}"





##################################################################
#
# Part 2: Generate Shared Files 
#
##################################################################

# Add log file
# ~2 h with 48GB
rule make16SShared:
	input:
		script="code/bash/mothurShared.sh",
		raw=expand('data/raw/{mothurSamples}_{readNum}_001.fastq.gz',
			mothurSamples = mothurSamples, readNum = readNum),
		refs=rules.get16SReferences.output
	output:
		shared="data/process/final.shared",
		taxonomy="data/process/final.taxonomy",
		errorFasta="data/process/errorinput.fasta",
		errorCount="data/process/errorinput.count_table"
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} data/raw/ {input.refs}"


# Add log file
# ~2 h with 48GB
rule split16SShared:
	input:
		script="code/bash/mothurSplitShared.sh",
		shared=rules.make16SShared.output.shared
	output:
		shared=expand("data/process/{group}.final.shared",
			group = mothurGroups)
	params:
		mockGroups='-'.join(mothurMock), # Concatenates all mock group names with hyphens
		controlGroups='-'.join(mothurControl) # Concatenates all control group names with hyphens
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} {params.mockGroups} {params.controlGroups}"


# Add log file
# ~5 min with 48GB
rule count16SShared:
	input:
		script="code/bash/mothurCountShared.sh",
		shared="data/process/{group}.final.shared"
	output:
		count="data/process/{group}.final.count.summary"
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} {input.shared}"


# Add log file
# ~5 min with 48GB
rule subsample16SShared:
	input:
		script="code/bash/mothurSubsampleShared.sh",
		shared="data/process/{group}.final.shared",
		count="data/process/{group}.final.count.summary"
	output:
		subsampleShared="data/process/{group}.final.0.03.subsample.shared"
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} {input.shared} {input.count}"





##################################################################
#
# Part 3: Diversity Metrics 
#
##################################################################

# Add log file
# ~15 min 48 GB
rule rarefy16SReads:
	input:
		script="code/bash/mothurRarefaction.sh",
		shared="data/process/sample.final.shared"
	output:
		rarefaction="data/process/sample.final.groups.rarefaction"
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} {input.shared}"


# Add log file
# ~15 min 48GB
rule calc16SAlphaDiversity:
	input:
		script="code/bash/mothurAlpha.sh",
		shared="data/process/sample.final.shared",
		count="data/process/sample.final.count.summary"
	output:
		alpha="data/process/sample.final.groups.ave-std.summary"
	params:
		alpha='-'.join(mothurAlpha) # Concatenates all alpha metric names with hyphens
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} {input.shared} {input.count} {params.alpha}"


# Add log file
# ~20 min 48GB
rule calc16SBetaDiversity:
	input:
		script="code/bash/mothurBeta.sh",
		shared="data/process/sample.final.shared",
		count="data/process/sample.final.count.summary"
	output:
		dist=expand("data/process/sample.final.{beta}.0.03.lt.ave.dist",
			beta = mothurBeta)
	params:
		beta='-'.join(mothurBeta) # Concatenates all beta metric names with hyphens
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} {input.shared} {input.count} {params.beta}"





##################################################################
#
# Part 4: Ordination 
#
##################################################################

# Add log file
# ~10 min 48GB
rule calc16SPCoA:
	input:
		script="code/bash/mothurPCoA.sh",
		dist="data/process/sample.final.{beta}.0.03.lt.ave.dist"
	output:
		loadings="data/process/sample.final.{beta}.0.03.lt.ave.pcoa.loadings",
		axes="data/process/sample.final.{beta}.0.03.lt.ave.pcoa.axes"
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} {input.dist}"


# Add log file
# ~10 min 48GB
rule calc16SNMDS:
	input:
		script="code/bash/mothurNMDS.sh",
		dist="data/process/sample.final.{beta}.0.03.lt.ave.dist"
	output:
		stress="data/process/sample.final.{beta}.0.03.lt.ave.nmds.stress",
		axes="data/process/sample.final.{beta}.0.03.lt.ave.nmds.axes"
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} {input.dist}"





##################################################################
#
# Part 5: Quality Control 
#
##################################################################

# Add log file
# ~10 min 48GB
rule calc16SError:
	input:
		script="code/bash/mothurError.sh",
		errorFasta=rules.make16SShared.output.errorFasta,
		errorCount=rules.make16SShared.output.errorCount,
		mockV4=rules.get16SMock.output.mockV4
	output:
		summary="data/process/error_analysis/errorinput.pick.error.summary"
	params:
		mockGroups='-'.join(mothurMock) # Concatenates all mock group names with hyphens
	conda:
		"envs/mothur.yaml"
	shell:
		"bash {input.script} {input.errorFasta} {input.errorCount} {input.mockV4} {params.mockGroups}"
